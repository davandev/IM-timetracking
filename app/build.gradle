apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def gitCommitHash = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 24
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId 'com.ingloriousmind.android.imtimetracking'
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 6
        versionName '1.2.0'

        buildConfigField "String", "GIT_COMMIT_HASH", "\"${gitCommitHash}\""
    }
    signingConfigs {
        release {
        }
    }
    if (project.hasProperty('storeFile') &&
            project.hasProperty('storePass') &&
            project.hasProperty('keyAlias') &&
            project.hasProperty('keyPass')) {
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePass
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPass
    } else {
        buildTypes.release.signingConfig = null
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }
}

ext {
    supportLibVersion = '24.0.0'
    daggerVersion = '2.4'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.google.dagger:dagger:${daggerVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'com.esotericsoftware:kryo:4.0.0'
    compile 'com.jakewharton.timber:timber:4.1.1'
    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'com.j256.ormlite:ormlite-android:4.48'
}
